# Copyright 2021 The Hafnium Authors.
#
# Use of this source code is governed by a BSD-style
# license that can be found in the LICENSE file or at
# https://opensource.org/licenses/BSD-3-Clause.

import("//build/image/image.gni")

config("config") {
  include_dirs = [
    "inc",
    "//test/inc",
  ]
}

source_set("common") {
  testonly = true
  public_configs = [
    ":config",
    "//src/arch/aarch64:arch_config",
  ]
  sources = [ "common.c" ]
}

source_set("setup_and_discovery") {
  testonly = true
  public_configs = [
    ":config",
    "//src/arch/aarch64:arch_config",
  ]
  sources = [ "setup_and_discovery.c" ]

  deps = [ ":common" ]
}

source_set("partition_services") {
  testonly = true
  public_configs = [
    ":config",
    "//src/arch/aarch64:arch_config",
  ]
  sources = [ "partition_services.c" ]

  deps = [
    ":common",
    "//test/vmapi/common:common",
  ]
}

source_set("notifications") {
  testonly = true
  public_configs = [
    ":config",
    "//src/arch/aarch64:arch_config",
  ]
  sources = [ "notifications.c" ]

  deps = [ ":common" ]
}

source_set("power_mgt") {
  testonly = true
  public_configs = [
    ":config",
    "//src/arch/aarch64:arch_config",
  ]
  sources = [ "power_mgt.c" ]
}

source_set("sp805") {
  testonly = true
  public_configs = [
    ":config",
    "//src/arch/aarch64:arch_config",
  ]
  sources = [ "sp805.c" ]
}

source_set("secure_interrupts") {
  testonly = true
  public_configs = [
    ":config",
    "//src/arch/aarch64:arch_config",
  ]
  sources = [ "secure_interrupts.c" ]
}

source_set("message_loop") {
  testonly = true
  public_configs = [
    ":config",
    "//test/hftest:hftest_config",
    "//src/arch/aarch64:arch_config",
  ]
  sources = [ "message_loop.c" ]

  deps = [
    ":notifications",
    ":partition_services",
    ":power_mgt",
    ":secure_interrupts",
    ":setup_and_discovery",
  ]
}

source_set("sp_helpers") {
  testonly = true
  public_configs = [
    ":config",
    "//src/arch/aarch64:arch_config",
  ]
  sources = [ "sp_helpers.c" ]
}

# SP that will be controlled via communication/scheduling primitives by the
# former SP
vm_kernel("secure_partitions_services") {
  testonly = true
  public_configs = [ ":config" ]

  deps = [
    ":message_loop",
    ":sp805",
    ":sp_helpers",
    "//src/arch/aarch64:arch",
    "//src/arch/aarch64/hftest:interrupts",
    "//test/hftest:hftest_secure_service",
  ]
}

manifest("partition_manifest_service_sp_first") {
  source = "partition_manifest_service_sp_first.dts"
  output = "partition_manifest_service_sp_first.dtb"
}

manifest("partition_manifest_service_sp_second") {
  source = "partition_manifest_service_sp_second.dts"
  output = "partition_manifest_service_sp_second.dtb"
}

manifest("partition_manifest_service_sp_third") {
  source = "partition_manifest_service_sp_third.dts"
  output = "partition_manifest_service_sp_third.dtb"
}

partition_package("service_sp_first_package") {
  testonly = true
  pm_offset = "0x1000"
  img_offset = "0x2000"
  files = [ [
        "partition_manifest_service_sp_first.dtb",
        "secure_partitions_services.bin",
        ":partition_manifest_service_sp_first",
        ":secure_partitions_services",
      ] ]
  output = "service_sp_first_package.img"
}

partition_package("service_sp_second_package") {
  testonly = true
  pm_offset = "0x1000"
  img_offset = "0x2000"
  files = [ [
        "partition_manifest_service_sp_second.dtb",
        "secure_partitions_services.bin",
        ":partition_manifest_service_sp_second",
        ":secure_partitions_services",
      ] ]
  output = "service_sp_second_package.img"
}

partition_package("service_sp_third_package") {
  testonly = true
  pm_offset = "0x1000"
  img_offset = "0x2000"
  files = [ [
        "partition_manifest_service_sp_third.dtb",
        "secure_partitions_services.bin",
        ":partition_manifest_service_sp_third",
        ":secure_partitions_services",
      ] ]
  output = "service_sp_third_package.img"
}
