# Copyright 2021 The Hafnium Authors.
#
# Use of this source code is governed by a BSD-style
# license that can be found in the LICENSE file or at
# https://opensource.org/licenses/BSD-3-Clause.

import("//build/image/image.gni")
import("//test/hftest/args.gni")

config("config") {
  include_dirs = [ "inc" ]
}

source_set("ffa") {
  public_configs = [
    "//test/vmapi/primary_with_secondaries:config",
    "//src/arch/aarch64:config",
    "//test/hftest:hftest_config",
  ]
  sources = [ "//test/vmapi/primary_with_secondaries/ffa.c" ]
}

source_set("memory_sharing") {
  public_configs = [
    "//test/vmapi/primary_with_secondaries:config",
    "//src/arch/aarch64:config",
    "//test/hftest:hftest_config",
  ]
  sources = [ "memory_sharing.c" ]
}

vm_kernel("el0_partition_test_vm") {
  testonly = true
  public_configs = [
    "//test/vmapi/primary_with_secondaries:config",
    "//src/arch/aarch64:config",
  ]

  sources = [
    "//test/vmapi/primary_with_secondaries/mailbox_common.c",
    "boot.c",
    "interrupts.c",
  ]

  deps = [
    ":ffa",
    ":memory_sharing",
    "//src/arch/aarch64/hftest:registers",
    "//test/hftest:hftest_primary_vm",
    "//test/vmapi/common",
    "//vmlib",
  ]
}

manifest("el0_partition_test_manifest") {
  source = "manifest.dts"
  output = "manifest.dtb"
  overlay = hftest_manifest_overlay
}

device_tree("secondary_dtb") {
  source = "secondary.dts"
  output = "$target_out_dir/secondary.dtb"
}

initrd("el0_partitions_test") {
  testonly = true

  files = [
    [
      "manifest.dtb",
      ":el0_partition_test_manifest",
      "manifest.dtb",
    ],
    [
      "el0_partition_test",
      ":el0_partition_test_vm",
      "el0_partition_test_vm.bin",
    ],
    [
      "services1",
      "services:service_vm1",
      "service_vm1.bin",
    ],
    [
      "services2",
      "services:service_vm2",
      "service_vm2.bin",
    ],
    [
      "services3",
      "services:service_vm3",
      "service_vm3.bin",
    ],
    [
      "secondary.dtb",
      ":secondary_dtb",
      "secondary.dtb",
    ],
  ]
}
